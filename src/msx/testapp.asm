SECTION code_user
PUBLIC _main

; --------------------------------------------------
; ドライバを使用するプログラムで必要
; --------------------------------------------------
EXTERN SOUNDDRV_INIT
EXTERN SOUNDDRV_EXEC
EXTERN SOUNDDRV_BGMPLAY
EXTERN SOUNDDRV_SFXPLAY
EXTERN SOUNDDRV_STOP
; --------------------------------------------------
; ここまで
; --------------------------------------------------


_main:
; ====================================================================================================
; テストアプリケーション
; ====================================================================================================
TESTAPP:
    CALL INIT
    CALL MAINLOOP
    JP TESTAPP


; ====================================================================================================
; 初期処理
; ====================================================================================================
INIT:
    ; ■画面初期化
    CALL SCREEN_INIT

    ; ■フォントパターン定義
    CALL SET_FONT_PATTERN

    ; ■文字列表示
    LD DE,STRDATA1
    CALL PRTFIXSTR
    LD DE,STRDATA2
    CALL PRTFIXSTR
    LD DE,STRDATA3
    CALL PRTFIXSTR
    LD DE,STRDATA4
    CALL PRTFIXSTR
    LD DE,STRDATA5
    CALL PRTFIXSTR
    LD DE,STRDATA6
    CALL PRTFIXSTR
    LD DE,STRDATA7
    CALL PRTFIXSTR
    LD DE,STRDATA8
    CALL PRTFIXSTR
    LD DE,STRDATA9
    CALL PRTFIXSTR
    LD DE,STRDATA10
    CALL PRTFIXSTR
    LD DE,STRDATA11
    CALL PRTFIXSTR
    LD DE,STRDATA12
    CALL PRTFIXSTR

    ; --------------------------------------------------
    ; ドライバを使用するプログラムで必要
    ; --------------------------------------------------
    ; ■ドライバ初期化
    ;   利用するアプリケーションで行う処理
    ;   割り込みフック書き換え前に行う
    CALL SOUNDDRV_INIT

    ; ■H.TIMI割り込みフック書き換え
    DI                              ; 割り込み禁止
    LD A,$C3                        ; JP
    LD HL,SOUNDDRV_EXEC             ; サウンドドライバのアドレス
    LD (H_TIMI+0),A
    LD (H_TIMI+1),HL
    EI                              ; 割り込み許可
    ; --------------------------------------------------
    ; ここまで
    ; --------------------------------------------------


; ====================================================================================================
; メイン
; ====================================================================================================
MAINLOOP:
    CALL GET_KEYMATRIX              ; キーマトリクス取得
    LD B,10                         ; 0〜9のキーの入力をチェック
    LD DE,KEYBUFF                   ; キーバッファの先頭アドレス

MAINLOOP_L1:
    LD A,(DE)                       ; A <- キーバッファの値
    OR A
    JR Z,MAINLOOP_L4                ; キーバッファの値がゼロなら次の処理へ

MAINLOOP_L2:
    LD A,B                          ; A < B(ループカウンタ)
    DEC A                           ; Bは1〜10なので、-1する

    LD C,A                          ; C <- A (計算用に値をコピー)
    ADD A,A                         ; A <- A*2+C = A*3
    ADD A,C

    LD HL,MAINLOOP_L3

    PUSH DE
    LD D,0                          ; DE <- A
    LD E,A
    ADD HL,DE                       ; Noに対応するJPのアドレスを求める
    POP DE
    JP (HL)

MAINLOOP_L3:
    JP PLAY9
    JP PLAY8
    JP PLAY7
    JP PLAY6
    JP PLAY5
    JP PLAY4
    JP PLAY3
    JP PLAY2
    JP PLAY1
    JP PLAY0

MAINLOOP_L4:
    INC DE
    DJNZ MAINLOOP_L1

	JR MAINLOOP


PLAY0:
    LD HL,$0000
    CALL WRTCUR

    ; --------------------------------------------------
    ; 演奏を止めるときは、SOUNDDRV_STOPをCALLする
    ; --------------------------------------------------
    CALL SOUNDDRV_STOP
    JP MAINLOOP_L4

PLAY1:
    LD HL,32*7+2
    CALL WRTCUR

    ; --------------------------------------------------
    ; BGMの演奏を開始するときは、HLにデータの先頭アドレスを設定し
    ; SOUNDDRV_BGMPLAYをCALLする
    ; --------------------------------------------------
    LD HL,MUSIC01
    CALL SOUNDDRV_BGMPLAY
    JP MAINLOOP_L4

PLAY2:
    LD HL,32*8+2
    CALL WRTCUR

    LD HL,_18
    CALL SOUNDDRV_BGMPLAY
    JP MAINLOOP_L4

PLAY3:
    LD HL,32*9+2
    CALL WRTCUR

    LD HL,_19
    CALL SOUNDDRV_BGMPLAY
    JP MAINLOOP_L4

PLAY4:
    LD HL,32*10+2
    CALL WRTCUR

    LD HL,_20
    CALL SOUNDDRV_BGMPLAY
    JP MAINLOOP_L4

PLAY5:
    LD HL,32*11+2
    CALL WRTCUR

    ; --------------------------------------------------
    ; SFXの演奏を開始するときは、HLにデータの先頭アドレスを設定し
    ; SOUNDDRV_SFXPLAYをCALLする
    ; --------------------------------------------------
    LD HL,SFX04
    CALL SOUNDDRV_SFXPLAY
    JP MAINLOOP_L4

PLAY6:
    LD HL,32*12+2
    CALL WRTCUR

    LD HL,SFX02
    CALL SOUNDDRV_SFXPLAY
    JP MAINLOOP_L4

PLAY7:
    LD HL,32*13+2
    CALL WRTCUR

    LD HL,SFX05
    CALL SOUNDDRV_SFXPLAY
    JP MAINLOOP_L4

PLAY8:
    LD HL,32*14+2
    CALL WRTCUR

    LD HL,SFX03
    CALL SOUNDDRV_SFXPLAY
    JP MAINLOOP_L4

PLAY9:
    LD HL,32*15+2
    CALL WRTCUR

    LD HL,SFX01
    CALL SOUNDDRV_SFXPLAY
    JP MAINLOOP_L4

WRTCUR:
    PUSH BC
    PUSH DE
    PUSH HL

    LD DE,CURDATA0
    LD HL,32*7+2
    CALL PRTSTR
    LD DE,CURDATA0
    LD HL,32*8+2
    CALL PRTSTR
    LD DE,CURDATA0
    LD HL,32*9+2
    CALL PRTSTR
    LD DE,CURDATA0
    LD HL,32*10+2
    CALL PRTSTR
    LD DE,CURDATA0
    LD HL,32*11+2
    CALL PRTSTR
    LD DE,CURDATA0
    LD HL,32*12+2
    CALL PRTSTR
    LD DE,CURDATA0
    LD HL,32*13+2
    CALL PRTSTR
    LD DE,CURDATA0
    LD HL,32*14+2
    CALL PRTSTR
    LD DE,CURDATA0
    LD HL,32*15+2
    CALL PRTSTR

    POP HL
    LD A,H
    OR L
    JR Z,WRTCUR_EXIT

    LD DE,CURDATA1
    CALL PRTSTR

WRTCUR_EXIT:
    POP DE
    POP BC
    RET


; ====================================================================================================
; キーマトリクス取得処理
; ====================================================================================================
GET_KEYMATRIX:

    LD HL,KEYBUFF                   ; HL <- キーバッファアドレス
    LD DE,KEYBUFF_SV                ; HL <- キー入力バッファSVの先頭アドレス

    LD A,0                          ; キーマトリクスの0行目をスキャン対象
    CALL SNSMAT                     ; BIOS キーマトリクススキャン
    CALL GET_KEYMATRIX_SUB          ; 入力キーの情報をバッファに設定

    LD A,1                          ; キーマトリクスの1行目をスキャン対象
    CALL SNSMAT                     ; BIOS キーマトリクススキャン
    CALL GET_KEYMATRIX_SUB          ; 入力キーの情報をバッファに設定

    RET


GET_KEYMATRIX_SUB:
    ; ■以下前提
    ;   - Aレジスタにスキャン結果のデータが入っている
    ;   - HLレジスタにキーバッファのアドレスが設定されている
    ;   - DEレジスタにキー入力バッファSVのアドレスが設定されている
    LD B,8                          ; 0〜7ビットをスキャンするためのループ回数
    LD C,A                          ; C <- A (値を退避)

GET_KEYMATRIX_SUB_L1:
    LD A,C                          ; A <- C (退避した値をAレジスタに戻す)
    AND %00000001                   ; 下位1ビットを判定
    JR NZ,GET_KEYMATRIX_SUB_L2      ; ビットが立っている=キーが押されていないならL2へ

    ; ■キーが押されているときの処理
    ;   キー入力バッファSVの値を読み、OFFの時だけキー入力バッファをONにする
    ;   キー入力バッファSVがONの時は押しっぱなしなので、キー入力バッファをOFFにする
    LD A,(DE)                       ; A <- キー入力バッファSV
    OR A
    JR Z,GET_KEYMATRIX_SUB_L12      ; キー入力バッファSVがOFFの時はL12へ

    LD (HL),KEY_OFF                 ; キー入力バッファにOFFを設定
                                    ; キー入力バッファSVはそのままで良いので何もしない
    JR GET_KEYMATRIX_SUB_L3

GET_KEYMATRIX_SUB_L12:
    LD (HL),KEY_ON                  ; キー入力バッファにONを設定
    LD A,KEY_ON
    LD (DE),A                       ; キー入力バッファSVにONを設定
    JR GET_KEYMATRIX_SUB_L3

GET_KEYMATRIX_SUB_L2:
    ; ■キーが押されていないときの処理
    LD (HL),KEY_OFF                 ; キーバッファワークにキーオフを設定
    LD A,KEY_OFF
    LD (DE),A                       ; キー入力バッファSVにOFFを設定

GET_KEYMATRIX_SUB_L3:
    INC HL                          ; キーバッファワークのアドレスを+1
    INC DE                          ; キー入力バッファSVのアドレスを+1
    SRL C                           ; Cレジスタの値を右シフト

    DJNZ GET_KEYMATRIX_SUB_L1

    RET


; ====================================================================================================
; 画面初期化
; ====================================================================================================
SCREEN_INIT:
    ; ■COLOR 15,1,1
    LD A,15                         ; Aレジスタに文字色をロード 
    LD (FORCLR),A                   ; Aレジスタの値をワークエリアに格納
    LD A,1                          ; Aレジスタに全景色をロード
    LD (BAKCLR),A                   ; Aレジスタの値をワークエリアに格納
;    LD A,1                         ; Aレジスタに背景色をロード
    LD (BDRCLR),A                   ; Aレジスタの値をワークエリアに格納

    ; ■SCREEN 1,2,0
    LD A,(REG1SAV)                  ; AレジスタにVDPコントロールレジスタ1の値をロード
    OR 2                            ; ビット2を立てる(=スプライトモードを16x16に設定)
    LD (REG1SAV),A                  ; Aレジスタの値をVDPコントロールレジスタ1のワークエリアに格納
    LD A,1                          ; Aレジスタにスクリーンモードの値を設定
    CALL CHGMOD                     ; BIOS スクリーンモード変更
    LD A,0                          ; Aレジスタにキークリックスイッチの値(0=OFF)をロード
    LD (CLIKSW),A                   ; Aレジスタの値をワークエリアに格納

    ; ■WIDTH 32
    LD A,32                         ; AレジスタにWIDTHの値を設定
    LD (LINL32),A                   ; Aレジスタの値をワークエリアに格納

    ; ■KEY OFF
    CALL ERAFNC                     ; BIOS ファンクションキー非表示

    RET


; ====================================================================================================
; フォントパターン定義
; ====================================================================================================
SET_FONT_PATTERN:
	LD HL,FONT_PTN_DATA			    ; HLレジスタに転送元データの先頭アドレスを設定
    LD DE,PTN_GEN_ADDR+32*8         ; DEレジスタに転送先アドレスを設定
	LD BC,8*64					    ; BCレジスタにデータサイズを指定
    CALL LDIRVM					    ; BIOS VRAMブロック転送

    RET


; ====================================================================================================
; 文字列固定位置表示サブルーチン
; IN  : DE = 表示文字データの開始アドレス
; HLレジスタを破壊します
; ====================================================================================================
PRTFIXSTR:
    LD A,(DE)                       ; DE <- HLアドレスの示す表示位置データ
    LD L,A
    INC DE
    LD A,(DE)
    LD H,A
    INC DE                          ; DE <- 文字列データの先頭アドレス


; ====================================================================================================
; 文字列表示サブルーチン
; IN  : HL = 表示位置（y*32+x）
;       DE = 表示文字データの開始アドレス
; BCレジスタを破壊します
; ====================================================================================================
PRTSTR:
    LD BC,PTN_NAME_ADDR             ; BC <- パターンネームテーブルの先頭アドレス
    ADD HL,BC                       ; HL=HL+BC

PRTSTR_L1:
	LD A,(DE)				        ; AレジスタにDEレジスタの示すアドレスのデータを取得
	OR 0					        ; 0かどうか
    JR Z,PRTSTR_END			        ; 0の場合はPRTENDへ

	CALL WRTVRM				        ; BIOS WRTVRM呼び出し
	    					        ; - HL : 書き込み先のVRAMアドレス
    	                            ; - A  : 書き込むデータ

	INC HL					        ; HL=HL+1
    INC DE					        ; DE=DE+1
    JR PRTSTR_L1

PRTSTR_END:
	RET


; ====================================================================================================
; 定数エリア
; romに格納される
; ====================================================================================================
SECTION rodata_user

; ■BIOSアドレス定義
INCLUDE "include/msxbios.inc"

; ■システムワークエリアアドレス定義
INCLUDE "include/msxsyswk.inc"

; ■VRAMワークエリアアドレス定義
INCLUDE "include/msxvrmwk.inc"

; ■キースキャン用定数
KEY_ON:                 EQU $01     ; キーオン
KEY_OFF:                EQU $00     ; キーオフ

; ■フォントパターン
INCLUDE "font.asm"

; ■表示文字列データ
; dw : 表示先のVRAMアドレスのオフセット値(下位/上位)    
; db : 表示文字列、最後に0を設定すること
STRDATA1:
    DW 32*3+6
	DB "MSX PSG DRIVER TEST",0
STRDATA2:
    DW 32*4+6
	DB "-------------------",0
STRDATA3:
    DW 32*7+3
	DB "[1] BGM(XEVIOUS)",0
STRDATA4:
    DW 32*8+3
	DB "[2] BGM(NEW RALLY-X BGMC)",0
STRDATA5:
    DW 32*9+3
	DB "[3] BGM(NEW RALLY-X BGMA)",0
STRDATA6:
    DW 32*10+3
	DB "[4] BGM(NEW RALLY-X BGMB)",0
STRDATA7:
    DW 32*11+3
	DB "[5] SFX(XEVIOUS CREDIT)",0
STRDATA8:
    DW 32*12+3
	DB "[6] SFX(XEVIOUS ZAPPER)",0
STRDATA9:
    DW 32*13+3
	DB "[7] SFX(XEVIOUS BLASTER)",0
STRDATA10:
    DW 32*14+3
	DB "[8] SFX(XEVIOUS DESTRUCTION)",0
STRDATA11:
    DW 32*15+3
	DB "[9] SFX(XEVIOUS BACURA)",0
STRDATA12:
    DW 32*17+3
	DB "[0] STOP",0
CURDATA0:
    DB " ",0
CURDATA1:
    DB ">",0


; ----------------------------------------------------------------------------------------------------
; 曲データ
; サウンドドライバを使用するプログラムで定義が必要
; ----------------------------------------------------------------------------------------------------

; ----------------------------------------------------------------------------------------------------
; ゼビウス　ゲーム中BGM
; ----------------------------------------------------------------------------------------------------
MUSIC01:
    DB  0                           ; BGMでは強制的に切り替わるのでプライオリティの設定は無効
    DW  MUSIC01_TRK1                ; ChAのトラックデータ
    DW  MUSIC01_TRK2                ; ChBのトラックデータ
    DW  $0000                       ; ChCのトラックデータ(なし)

MUSIC01_TRK1:
    DB  201,%10                     ; Noise/ToneSw 0:On,1:Off
    DB  200, 14                     ; ボリューム
    DB   48,  6, 60, 6, 59, 6, 60, 6
    DB   64,  6, 60, 6, 59, 6, 60, 6
    DB   48,  6, 60, 6, 58, 6, 60, 6
    DB   64,  6, 60, 6, 58, 6, 60, 6
    DB   48,  6, 60, 6, 57, 6, 60, 6
    DB   64,  6, 60, 6, 57, 6, 60, 6
    DB   48,  6, 60, 6, 56, 6, 60, 6
    DB   64,  6, 60, 6, 56, 6, 60, 6
    DB  254,  0

MUSIC01_TRK2:
    DB  201,%10                     ; Noise/ToneSw 0:On,1:Off
    DB  200, 12                     ; ボリューム
    DB  210,  1                     ; デチューン値
    DB   48,  6, 60, 6, 59, 6, 60, 6
    DB   64,  6, 60, 6, 59, 6, 60, 6
    DB   48,  6, 60, 6, 58, 6, 60, 6
    DB   64,  6, 60, 6, 58, 6, 60, 6
    DB   48,  6, 60, 6, 57, 6, 60, 6
    DB   64,  6, 60, 6, 57, 6, 60, 6
    DB   48,  6, 60, 6, 56, 6, 60, 6
    DB   64,  6, 60, 6, 56, 6, 60, 6
    DB  254,  0

MUSIC01_TRK3:
    DB  255

; ----------------------------------------------------------------------------------------------------
; バキュラにザッパーが命中した音
; ----------------------------------------------------------------------------------------------------
SFX01:
    DB  10                          ; プライオリティ (0:低、255:高)
    DW  SFX01_TRK1                  ; chAのトラックデータ
    DW  SFX01_TRK2                  ; chBのトラックデータ
    DW  SFX01_TRK3                  ; chCのトラックデータ

SFX01_TRK1:
    DB  201,%10                     ; Noise/ToneSw 0:On,1:Off
    DB  200, 15
    DB   60,  3, 200, 10, 60,  3,200,  8, 60,  3,200,  6, 60,  3, 200,  4, 60,  3
    DB  200,  0
    DB  255 

SFX01_TRK2:
    DB  201,%10                     ; Noise/ToneSw 0:On,1:Off
    DB  200, 13
    DB  210,  2
    DB   60,  3, 200,  8, 60,  3,200,  6, 60,  3,200,  4, 60,  3, 200,  2, 60,  3
    DB  200,  0
    DB  255

SFX01_TRK3:
    DB  201,%10                     ; Noise/ToneSw 0:On,1:Off
    DB  202,  1
    DB  200, 15
    DB   61,  3
    DB  200,  0
    DB  255

; ----------------------------------------------------------------------------------------------------
; ゼビウス ザッパー発射音
; ----------------------------------------------------------------------------------------------------
SFX02:
    DB  5                           ; プライオリティ (0:低、255:高)
    DW  $0000                       ; chAのトラックデータ
    DW  $0000                       ; chBのトラックデータ
    DW  SFX02_TRK3                  ; chCのトラックデータ

SFX02_TRK1:
    DB  255 

SFX02_TRK2:
    DB  255 

SFX02_TRK3:
    DB  201,%10                     ; Noise/ToneSw 0:On,1:Off
    DB  200, 15
;    DB   18,  1,  66,  1, 19,  1, 61,  1, 20,  1, 56,  1, 21,  1, 51,  1 
    DB   18,  1,  78,  1, 21,  1, 63,  1 
    DB  200,  0
    DB  255

; ----------------------------------------------------------------------------------------------------
; ゼビウス 敵飛行隊破壊音
; ----------------------------------------------------------------------------------------------------
SFX03:
    DB  9                           ; プライオリティ (0:低、255:高)
    DW  SFX03_TRK1                  ; chAのトラックデータ
    DW  SFX03_TRK2                  ; chBのトラックデータ
    DW  SFX03_TRK3                  ; chCのトラックデータ

SFX03_TRK1:
    DB  201,%10                     ; Noise/ToneSw 0:On,1:Off
    DB  202,  1
    DB  200, 15
    DB   93,  3,  91,  3, 95,  3, 93,  3 
    DB  200,  0
    DB  255

SFX03_TRK2:
    DB  201,%10                     ; Noise/ToneSw 0:On,1:Off
    DB  202,  1
    DB  200, 15
    DB   90,  3,  88,  3, 92,  3, 90,  3 
    DB  200,  0
    DB  255 

SFX03_TRK3:
    DB  201,%10                     ; Noise/ToneSw 0:On,1:Off
    DB  202,  1
    DB  200, 15
    DB   84,  3,  85,  3, 86,  3, 87,  3 
    DB  200,  0
    DB  255

; ----------------------------------------------------------------------------------------------------
; ゼビウス クレジット音
; ----------------------------------------------------------------------------------------------------
SFX04:
    DB  200                         ; プライオリティ (0:低、255:高)
    DW  SFX04_TRK1                  ; chAのトラックデータ
    DW  SFX04_TRK2                  ; chBのトラックデータ
    DW  SFX04_TRK3                  ; chCのトラックデータ

SFX04_TRK1:
    DB  201,%10                     ; Noise/ToneSw 0:On,1:Off
    DB  200, 15                     ; Volume
    DB   20,  1, 19,  1, 18,  1, 17,  1, 16,  1, 15,  1, 14,  1, 13,  1 
    DB   32,  1, 31,  1, 30,  1, 29,  1, 28,  1, 27,  1, 26,  1, 25,  1
    DB   44,  1, 43,  1, 42,  1, 41,  1, 40,  1, 39,  1, 38,  1, 37,  1
    DB   56,  1, 55,  1, 54,  1, 53,  1, 52,  1, 51,  1, 50,  1, 49,  1
    DB  200,  0
    DB  255

SFX04_TRK2:
    DB  201,%10                     ; Noise/ToneSw 0:On,1:Off
    DB  200, 15                     ; Volume
    DB   19,  1, 18,  1, 17,  1, 16,  1, 15,  1, 16,  1, 15,  1, 14,  1
    DB   31,  1, 30,  1, 29,  1, 28,  1, 27,  1, 26,  1, 25,  1, 24,  1
    DB   43,  1, 42,  1, 41,  1, 40,  1, 39,  1, 38,  1, 37,  1, 36,  1
    DB   55,  1, 54,  1, 53,  1, 52,  1, 51,  1, 50,  1, 49,  1, 48,  1
    DB  200,  0
    DB  255 

SFX04_TRK3:
    DB  201,%00                     ; Noise/ToneSw 0:On,1:Off
    DB  202, 28                     ; NoiseTone
    DB  200, 13,  8,  1,200, 15,  8,  2,200, 13,  8,  2,200, 11,  8,  2
    DB  200,  6,  8,  2,200,  4,  8,  2,200,  2,  8,  2,200,  8,  8,  2
    DB  201,%11
    DB  255

; ----------------------------------------------------------------------------------------------------
; ゼビウス ブラスター発射音
; ----------------------------------------------------------------------------------------------------
SFX05:
    DB  5                           ; プライオリティ (0:低、255:高)
    DW  $0000                       ; chAのトラックデータ
    DW  $0000                       ; chBのトラックデータ
    DW  SFX05_TRK3                  ; chCのトラックデータ

SFX05_TRK1:
    DB  255

SFX05_TRK2:
    DB  255

SFX05_TRK3:
    DB  201,%10                     ; Noise/ToneSw 0:On,1:Off
    DB  200, 15                     ; Volume
    DB   48,  1, 49,  1, 50,  1, 51,  1, 52,  1, 51,  1, 50,  1, 49,  1, 48,  1
    DB   47,  1, 46,  1, 45,  1, 44,  1, 43,  1, 42,  1, 41,  1, 40,  1, 39,  1, 38,  1, 37,  1, 36,  1
    DB   35,  1, 34,  1, 33,  1, 32,  1, 31,  1, 30,  1, 29,  1, 28,  1, 27,  1, 26,  1, 25,  1, 24,  1
    DB   23,  1, 22,  1, 21,  1, 20,  1, 19,  1, 18,  1, 17,  1, 16,  1, 15,  1, 14,  1, 13,  1, 12,  1
    DB  200,  0
    DB  255 

; ----------------------------------------------------------------------------------------------------
; ラリーX BGM C
; ----------------------------------------------------------------------------------------------------
INCLUDE "18.asm"

; ----------------------------------------------------------------------------------------------------
; ラリーX BGM A
; ----------------------------------------------------------------------------------------------------
INCLUDE "19.asm"

; ----------------------------------------------------------------------------------------------------
; ラリーX BGM B
; ----------------------------------------------------------------------------------------------------
INCLUDE "20.asm"


; ====================================================================================================
; ワークエリア
; プログラム起動時にcrtでゼロでramに設定される 
; ====================================================================================================
SECTION bss_user

; ----------------------------------------------------------------------------------------------------
; その他ワークエリア
; ----------------------------------------------------------------------------------------------------
; ■キー入力バッファ
KEYBUFF:
    DB  $00,$00,$00,$00,$00,$00,$00,$00
    DB  $00,$00,$00,$00,$00,$00,$00,$00
KEYBUFF_SV:
    DB  $00,$00,$00,$00,$00,$00,$00,$00
    DB  $00,$00,$00,$00,$00,$00,$00,$00

